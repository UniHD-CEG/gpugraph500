reset
unset key
set xtics rotate by -45
set ylabel "time in s"	  
set key outside
set grid y
set style fill solid 0.50 border lt -1
set style histogram rowstacked 
set style data histograms

set title "Runtime share of several G500 activites\n Scalefactor $SCALE"
set key autotitle columnhead
set terminal jpeg size 1024,576
set output "rs_act_s$SCALE.jpg"

mpi=0
gpu=0
exp=0
total=0
stats '$datpath/scale_$SCALE.dat' u (mpi=$1):(stringcolumn(2) eq '-' ? gpu=0 : gpu=$2)	every ::0::0 noout
stats '$datpath/scale_$SCALE.dat' u (total=$4):(exp=$5) every ::0::0 noout

plot '$datpath/scale_$SCALE.dat' using 5:xtic(stringcolumn(2) eq '-' ? sprintf("%d Nodes", $1 ): sprintf("%d Nodes, %d GPU(s)", $1, $2)), \
				'' using 6, \
				'' using 4 t "total time" w histeps lw 3, \
				'' using (stringcolumn(2) eq '-' ? (mpi*exp)/$1 : (gpu*mpi*exp)/($1*$2) ) t "expansion - perfect scaling" w lp lw 3	,\
				'' using (stringcolumn(2) eq '-' ? (mpi*total)/$1 : (gpu*mpi*total)/($1*$2) ) t "total time - perfect scaling" w lp lw 3	
				
